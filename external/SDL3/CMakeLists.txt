include(FetchContent)

if(WIN32)
    set(SDL_REQVER 3.4.0)

    if(MSVC)
        FetchContent_Declare(download_sdl3 URL "https://github.com/libsdl-org/SDL/releases/download/release-${SDL_REQVER}/SDL3-devel-${SDL_REQVER}-VC.zip" DOWNLOAD_EXTRACT_TIMESTAMP)
        FetchContent_MakeAvailable(download_sdl3)
    elseif(MINGW)
        FetchContent_Declare(download_sdl3 URL "https://github.com/libsdl-org/SDL/releases/download/release-${SDL_REQVER}/SDL3-devel-${SDL_REQVER}-mingw.zip" DOWNLOAD_EXTRACT_TIMESTAMP)
        FetchContent_MakeAvailable(download_sdl3)
    else()
        message(FATAL_ERROR "SDL3 doesn't provide Windows binaries for your build environment!")
    endif()
    
    set(SDL3_DIR "${download_sdl3_SOURCE_DIR}/cmake")
endif()

find_package(SDL3 REQUIRED CONFIG REQUIRED COMPONENTS SDL3)

## Interface the package into a target
## that can be used anywhere in the project
add_library(external_SDL3 INTERFACE)
target_link_libraries(external_SDL3 INTERFACE SDL3::SDL3)

## Copy over the DLL
if(WIN32 AND TARGET SDL3::SDL3)
    get_target_property(SDL3_DLL_LOCATION SDL3::SDL3 IMPORTED_LOCATION)

    if(SDL3_DLL_LOCATION AND EXISTS "${SDL3_DLL_LOCATION}")
        add_custom_target(external_SDL3_DLL
            COMMAND ${CMAKE_COMMAND} -E copy_if_different "${SDL3_DLL_LOCATION}" "${PROJECT_BINARY_DIR}")
        add_dependencies(external_SDL3 external_SDL3_DLL)
    endif()
endif()

## Add an alias library so dependency list looks nicer
add_library(external::SDL3 ALIAS external_SDL3)
